use crate::bencode::{Token, Bencode, BencError};

grammar<'i>(input: &'i str);

pub Benc: Bencode<'i> = {
    "i" <Num> "e"         =>  Bencode::Num(<>),
    Str                   =>  Bencode::Str(<>),
    "l" <Benc*> "e"       =>  Bencode::List(<>),
    "d" <(Str Benc)*> "e" =>? Bencode::decode_dict(<>),
}

extern {
    type Location = usize;
    type Error = BencError;

    enum Token<'i> {
        "i" => Token::I,
        "l" => Token::L,
        "d" => Token::D,
        "e" => Token::E,
        ":" => Token::Colon,
        Num => Token::Num(<i64>),
        Str => Token::Str(<&'i str>),
    }
}
